#!/bin/zsh
set -euo pipefail

repo_dir=${0:A:h:h}
version=$(tr -d '[:space:]' < "$repo_dir/VERSION")
screenshots="$repo_dir/docs/screenshots"
capture_dir=$(mktemp -d)
trap 'rm -rf "$capture_dir"' EXIT

cd "$repo_dir"
swift build -Xswiftc -warnings-as-errors
binary="$repo_dir/.build/debug/Nuncid"

capture_probe() {
  local stem=$1
  shift
  local output="$capture_dir/$stem-$version.png"
  "$binary" "$@" "$output" >/dev/null 2>&1 &
  local probe_pid=$!
  local probe_try
  for probe_try in {1..100}; do
    [[ -s "$output" ]] && break
    sleep 0.1
  done
  kill "$probe_pid" 2>/dev/null || true
  wait "$probe_pid" 2>/dev/null || true
  [[ -s "$output" ]] || { print -u2 "Capture failed: $stem"; return 1; }
}

capture_probe pinned-card --overlay-probe --overlay-stress-probe --overlay-capture-probe
capture_probe inspection-unpinned --overlay-probe --overlay-stress-probe --overlay-temporary-probe --overlay-capture-probe
capture_probe inspection-zoom30 --overlay-probe --overlay-stress-probe --overlay-zoom-probe 30 --overlay-capture-probe
capture_probe inspection-zoom300 --overlay-probe --overlay-stress-probe --overlay-zoom-probe 300 --overlay-capture-probe
capture_probe workflow-run --overlay-probe --overlay-run-probe --overlay-capture-probe
capture_probe lookup-highlight --lookup-highlight-capture-probe
capture_probe settings-scanning --settings-capture-probe
capture_probe settings-markers --settings-markers-probe --settings-capture-probe
capture_probe settings-pinned --settings-pinned-probe --settings-capture-probe
capture_probe settings-appearance --settings-appearance-probe --settings-capture-probe
capture_probe version-history --version-history-probe --version-history-capture-probe
capture_probe version-history-dark --version-history-probe --version-history-dark-probe --version-history-capture-probe

# Synthetic rendering only: these probes never drag a file, open System Settings,
# request a privacy grant, or restart the installed app.
"$binary" --permission-capture-probe "$capture_dir/permission-guide-$version.png" "$capture_dir/Nuncid.app"
"$binary" --permission-helper-probe --permission-capture-probe "$capture_dir/permission-helper-$version.png" "$capture_dir/Nuncid.app"

for stem in pinned-card inspection-unpinned inspection-zoom30 inspection-zoom300 workflow-run lookup-highlight settings-scanning settings-markers settings-pinned settings-appearance version-history version-history-dark permission-guide permission-helper; do
  mv "$capture_dir/$stem-$version.png" "$screenshots/$stem-$version.png"
done
print -r -- "Captured Nuncid $version release visuals"
